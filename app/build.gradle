// Manifest version information!
def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
// bump for dogfood builds, public betas, etc.
def versionBuild = 1

tasks.whenTaskAdded { task ->
  if (task.name.contains("lint")) {
    task.enabled = false
  }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
  compileSdkVersion 25
  buildToolsVersion "25.0.1"

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 25

    versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  productFlavors {
    internal {
      applicationId "com.jemaystermind.tinkerdagger.internal"
    }
    production {
      applicationId "com.jemaystermind.tinkerdagger"
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    abortOnError false
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  /** --- UI --- **/
  compile 'com.android.support:appcompat-v7:25.0.1'
  compile 'com.android.support:support-annotations:25.0.1'

  compile 'com.jakewharton:butterknife:8.4.0'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

  /** --- Utilities --- **/
  compile 'com.google.dagger:dagger:2.8'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.8'

  /** --- Testing --- **/
  compile 'com.jakewharton.timber:timber:4.3.1'

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  testCompile 'junit:junit:4.12'
}
